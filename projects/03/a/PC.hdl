// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Not(in=reset, out=notreset);
    Not(in=load, out=notload);
    And(a=notreset, b=load, out=setload);
    And3Way(in[0]=notreset, in[1]=notload, in[2]=inc, out=setinc);
    
    // define filters based on control bit combo
    Mux16(a=dffOut, b=false, sel=reset, out=outReset);
    Mux16(a=outReset, b=in, sel=setload, out=outLoad);
    Inc16(in=outLoad, out=inc16out);
    Mux16(a=outLoad, b=inc16out, sel=setinc, out=outInc);
    
    // delay filtered result till next cycle
    Register(in=outInc,load=true, out=dffOut, out=out);
}
